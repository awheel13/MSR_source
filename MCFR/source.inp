% source term simple MSR
/*This is a  model of the:
Molten Salt Reactor source term paper
Grad Students: Alex Wheeler, Vik Singh
Advisor: Dr. Laurence Miller, Dr. Ondrej Chvala
*/

%______________surface definitions__________________________________
surf 201 sph   0   0   0   300    % fuel salt
surf 202 sph   0   0   0   450    % reflector 

%______________cell definitions_____________________________________
cell 11  0  salt      -201        % fuel salt 
cell 31  0  refl       201 -202   % recursive sleeve 
cell 99  0  outside    202        % graveyard   

%______________material definitions_________________________________
% fuel
mat salt -3.509643051 burn 1 vol 1.13e8 
rgb 100 255 100 
17037.09c -0.3707532563
11023.09c -0.0633565017
92235.09c -0.061083941467999996
92238.09c -0.49422461733199996

%  iron reflector [ density 7.874/((1+680*12e-6)^3) ]
mat refl   -7.68435 tmp 900 rgb 128 128 128
26054.09c  -0.058450   %  Fe
26056.09c  -0.917540   %  Fe
26057.09c  -0.021190   %  Fe
26058.09c  -0.002820   %  Fe

% boundary condition
set bc 1

set pop 10000 250 50
set acelib "sss_endfb7u.sssdir"
set declib "sss_endfb7.dec"
set nfylib "sss_endfb7.nfy"

% analog reaction rate
set arr 2

%plot 3 1500 1500
%mesh 3 1500 150

%___________Reprocessing/depletion___________
%Firstly, you need some extra materials to do depletion and reprocessing correctly.

%stockpile of extra U
mat U_stock -3.5096 burn 1 vol 1e8
17037.09c -0.3707532563
11023.09c -0.0633565017
92235.09c -0.05553085588
92238.09c -0.49977770291999996

%tanks for offgases
mat offgastankcore 0.0007 burn 1 vol 1e6
2004.09c 1

% overflow tank
mat overflow 0.0007 burn 1 vol 1e8
2004.09c 1

%_______mass flow definitions______

mflow U_in
all 0.0

mflow offgasratecore
Ne 1e-2
Ar 1e-2
He 1e-2
Kr 1e-2
Xe 1e-2
Rn 1e-2

% need to account for the increase in vloume with refueling
mflow over
all 0.0

set pcc 0 %predictor-corrector must be turned off to use depletion
set gcu -1 %turning off group constant generation hastens the calculation
set depmtx 1 %dumps depletion matrices if needed. should be one per burnt material.

%syntax:
% rc <from_mat> <to_mat> <mflow> <setting> where setting is either 0, 1 or 2.

rep source_rep
rc U_stock salt U_in 0
rc salt offgastankcore offgasratecore 1
rc salt overflow over 1

set printm 1 %prints out isotopic compositions of burnt materials over depletion
set powdens 2E-2 % power density in [kW/g] 

dep
pro source_rep
daystep
0.0208 0.0208 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 
7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 7 

%burn up for one year

set inventory
1
86
fp
lanthanides
actinides